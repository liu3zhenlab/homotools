#!/usr/bin/perl -w
#======================================================================
# homostack
#
# Author: Sanzhen Liu <liu3zhen@ksu.edu>
# 5/4/2021
#
# to stack alignments of multiple homologous sequences
#======================================================================

use strict;
use warnings;
use Getopt::Long;
use File::Temp;
use FindBin;
use Term::ANSIColor qw(:constants);
use FindBin;
use lib "$FindBin::Bin/lib";

my $version = "0.3.5";
# 9/25/2024: added --annotskip --alnskip, --ptitle, --seqheight, --cleanup 


my $identity = 80; 
my $match = 100;
my $prefix = "hsout";
my $threads = 1;
my $bandcol = "bisque3";
my $seqheight = 0.1;
my $minident = "auto";
my $maxident = "auto";
my $title = "ALNStack";

sub prompt {
    print <<EOF;
    Usage: perl $0 --seq <fasta> --annot <annot_file> [options]
    [Options]
    --seq <file>     fasta file containing a sequence as the query; required
                     multiple sequences can be input by using --seq multiple times
    --annotskip      skip annotation if specified; NO skipping by default
    --annot <file>   bed file to highlight regions in query; if --annotskip is specified, --annot will be ignored; required otherwise
                     [format]: 7 columns separated by Tab
                               chr start(0-based) end(1-based) label height(e.g., 0.1) strand(+/-) color(R compatible)
                     [NOTE 1]: if no --annotskip, the number --annot needs to match the number of --seq;
                               they will be paired by their order, i.e., 1st --seq paired with 1st --annot;
                               if some --annot has no data, input "none".
                     [NOTE 2]: "height" is the ratio of height of highlighted bars to height of each alignment unit
                               a highlighted bar fills the specified region if the height equals --\$seqheight value
    --alnskip        skip alignments if specified; NO skipping by default
    --identity <int> minimal percentage of identity from 0 to 100 ($identity)
    --match <int>    minimal bp match of an alignment ($match)
    --prefix <str>   the output directory and the prefix for output files ($prefix)
    --title <str>    the title of the plot ($title)
    --minident <int> lowest identity for plotting color scaling, 0-100 or auto (auto)
    --maxident <int> highest identity for plotting color scaling, 0-100 or auto (auto)
    --threads <int>  number of cpus ($threads)
    --seqheight <float> ratio of height of a sequence to height of each alignment unit ($seqheight)
    --bandcol <str>  a valid R color name ($bandcol)
    --cleanup        clean up outputs if specified; NO cleanup by default
    --version        version information
    --help:          help information.
EOF
exit;
}

###############################################
# parameters:
###############################################
my %opts = ();
my ($query, $db);

&GetOptions(\%opts, "seq=s@", "annot=s@",
                    "alnskip", "title=s",
                    "identity=i", "match=i",
                    "prefix=s", "threads=i",
					"bandcol=s", "seqheight=f",
                    "minident=i", "maxident=i",
                    "annotskip", "cleanup",
					"version", "help");

if (exists $opts{version}) {
	print "$version\n";
	exit;
}

&prompt if exists $opts{help} or !%opts;
my (@seq, @annot);

if (!exists $opts{seq} or (!exists $opts{annot} and !exists $opts{annotskip})) {
	print STDERR RED, "Both --seq and --annot/--annotskip are required\n", RESET;
	&prompt;
} else {
	@seq = @{$opts{seq}} if exists $opts{seq};
	if (exists $opts{annotskip}) { # annotation will be skipped
		for (my $i=0; $i<=$#seq; $i++) {
			push(@annot, "none")
		}
	} else {
		@annot = @{$opts{annot}} if exists $opts{annot};
	}
	if ($#seq != $#annot) {
		print STDERR RED, "Numbers of --seq and --annot MUST be equal\n", RESET;
		&prompt;
	}
}

$identity = $opts{identity} if exists $opts{identity};
$match = $opts{match} if exists $opts{match};
$prefix = $opts{prefix} if exists $opts{prefix};
$title = $opts{title} if exists $opts{title};
$threads = $opts{threads} if exists $opts{threads};
$seqheight = $opts{seqheight} if exists $opts{seqheight};
$bandcol = $opts{bandcol} if exists $opts{bandcol};
$maxident = $opts{maxident} if exists $opts{maxident};
$minident = $opts{minident} if exists $opts{minident};

###############################################
# preparation
###############################################
# create a directory for outputs
if (-d $prefix) {
	print STDERR RED, "Warning: the directory $prefix exists.\n", RESET;
} else {
	`mkdir $prefix`;
}

# script path:
my $scriptPath = $FindBin::Bin;
my $utilsPath = $scriptPath."/utils/";


&runreport("Start to run homostack");

###############################################
# check requirments
###############################################
&cmd_check("nucmer");
&cmd_check("R");
&cmd_check("realpath");
&cmd_check("show-coords");

&runreport("step 01: requirement checked");

###############################################
# intermediate output
###############################################
my $data_list = $prefix."/".$prefix.".1.data.list";
open(OUT, ">", $data_list) || die;
print OUT "order\tnucmber\tref\trefbed\tqry\tqrybed\n";

my $nucmer_prefix = $prefix."/".$prefix.".2.nucmer.";
my $pdfout = $prefix."/".$prefix.".3.alnstack.pdf";

###############################################
# compare via nucmer
###############################################
for (my $i=0; $i<$#seq; $i++) {
	my $current_seq = &re2abs($seq[$i]);
	my $current_bed;
	if ($annot[$i] eq "none") {
		$current_bed = "none";
	} else {
		$current_bed = &re2abs($annot[$i]);
	}
	my $next_seq = &re2abs($seq[$i + 1]);
	my $next_bed;
	if ($annot[$i + 1] eq "none") {
		$next_bed = "none";
	} else {
		$next_bed = &re2abs($annot[$i + 1]);
	}
	my $delta_out = $nucmer_prefix.$i.".delta";
	my $nucmer_show = $nucmer_prefix.$i.".txt";

	if (! exists $opts{alnskip}) {
		`nucmer --mum --delta $delta_out --threads $threads $current_seq $next_seq`;
		my $nucmer_show_header = "sstart\tsend\tqstart\tqend\tsmatch\tqmatch\tidentity\tslen\tqlen\tsubj\tqry";
		&strout2file($nucmer_show_header, $nucmer_show);
		`show-coords -HTl -I $identity -L $match $delta_out >> $nucmer_show`;
		# cleanup
		`rm $delta_out`;
	} else {
		if (!-f $nucmer_show) { 
			&runreport("step 02: sequences alignments skipped but some alignment files are missing");
			exit;
		}
	}
	# collect alingment file information
	print OUT "$i\t$nucmer_show\t$current_seq\t$current_bed\t$next_seq\t$next_bed\n";
}
close OUT;

&runreport("step 02: sequence alignments are ready");

###############################################
# alignment plot
###############################################
my $stackplot_r = $utilsPath."/stackplot.R";
my $plot_script =
"Rscript $stackplot_r \\
         $data_list \\
         $bandcol \\
         $minident \\
         $maxident \\
         $seqheight \\
         \"$title\" \\
         $pdfout";
print STDERR "  - Here is the R script for plotting\n";
print STDERR "    $plot_script\n";
# run plotting script
`$plot_script`;

&runreport("step 03: alignment plotted");

###############################################
# cleanup
###############################################
if (exists $opts{cleanup}) {
	`rm $data_list`;
	`rm $nucmer_prefix*.txt`;
}

###############################################
# module 1: check command availability
###############################################
sub cmd_check {
	my $cmd = shift;
	my $cmdPath=`which $cmd 2>/dev/null`;
	if (!$cmdPath) {# not founded
		print STDERR RED, "  $cmd is not found\n", RESET;
		print STDERR RED, "Exit\n", RESET;
		exit;
	}
}

###############################################
# module 2: report running result
###############################################
# funtion to report running return
sub runreport {
	my $injob = shift;
    my $dateinfo = `date +'o %Y-%m-%d %H:%M:%S'`;
	print STDERR MAGENTA, "$dateinfo", RESET;
	print STDERR "  $injob.\n";
}

###############################################
## module 3: convert relative path to absolute path
###############################################
sub re2abs {
	my $inpath = shift;
	my $outpath = `realpath $inpath`;
	chomp $outpath;
	return $outpath;
}

###############################################
# module 4: strout2file
###############################################
sub strout2file {
    my ($in_str, $out_filename) = @_;
    open my $strout_fh, '>', $out_filename || die;
    printf $strout_fh $in_str;
    print $strout_fh "\n";
    close $strout_fh;
}

